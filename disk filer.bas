!--------------------------------------------------
!- Friday, November 15, 2019 1:29:57 PM
!- Import of : 
!- c:\dev\_cbm\work\disk filer.prg
!- Commodore 64
!--------------------------------------------------
3 CO=0:DV=PEEK(186):DR=0:D0=DV:D1=DR:F0=PEEK(53280):F1=PEEK(53281)
4 IFPEEK(49152)<>76THENLOAD"ssort.ml",DV,1
5 AD=49152:A1=1020:A2=1021:A3=1022:A4=1023:A5=255:GOTO10
6 PRINT"{home}{down*12}":RETURN
7 GOSUB6:FORI=229TO242:POKEI,PEEK(I)OR128:NEXTI:GOTO510
10 POKE53280,14:POKE53281,6:PRINT"{light blue}";:DIMF$(1000,1),FL$(300),FL(300)
20 F=0:SP$="{space*35}"
30 PRINT"{clear}{ct n}{down*20}Disk Filer v1.0"
40 PRINT"Bo Zimmerman"
50 PRINT"{dark gray}o{gray}o{light gray}o{light blue}Planet Ink"
60 PRINT"San Marcos, Tx{space*2}3/4/93"
70 FORI=1TO1500:NEXTI
80 PRINT"{up}"SP$:PRINT"{up*2}"SP$:PRINT"{up*2}"SP$:PRINT"{down*20}"
100 PRINT"{clear}{down}Disk Filer 64{down}"
110 PRINT"[{cyan}L{light blue}] Load a new list"
120 PRINT"[{cyan}S{light blue}] Save current list"
130 PRINT"[{cyan}H{light blue}] Add entries by hand"
140 PRINT"[{cyan}C{light blue}] Catalog new entries"
150 PRINT"[{cyan}E{light blue}] Edit/delete an entry"
160 PRINT"[{cyan}D{light blue}] Disk interface"
170 PRINT"[{cyan}V{light blue}] View your list"
180 PRINT"[{cyan}A{light blue}] Alphabetize the list"
190 PRINT"[{cyan}P{light blue}] Print this list"
200 PRINT"[{cyan}X{light blue}] Exit back to basic"
500 C=1:GOSUB6
510 SL=1104+(40*C)+1:POKESL,PEEK(SL)+128
520 GETA$
530 IFA$="{up}"ANDC>1THENC=C-1:GOTO590
540 IFA$="{down}"ANDC<10THENC=C+1:GOTO590
560 IFA$<>CHR$(13)THEN520
570 POKESL,PEEK(SL)-128:ONCGOTO1000,2000,3000,4000,5000,6000,7000,8000,9000
580 POKE53280,F0:POKE53281,F1:PRINT"{clear}{142}";:END
590 POKESL,PEEK(SL)-128:GOTO510
600 GOSUB6:PRINT"{down}Reading Directory...":FL=1:I=FRE(0)
610 OPEN2,DV,0,"$"+MID$(STR$(DR),2):POKEA5,34:SYSAD:POKEA5,0:SYSAD
620 POKEA5,34:SYSAD
630 X=0:F$="":IFSTTHENCLOSE2:FL=FL-1:PRINT"{up}";SP$:GOTO660
640 X=X+1:GET#2,A$:F$=F$+A$:IFA$<>CHR$(34)THEN640
650 FL$(FL)=LEFT$(F$,LEN(F$)-1):FL=FL+1::F$="":POKEA5,0:SYSAD:GOTO620
660 IFFL=0THENPRINT"{up}No Files!{up}":FORI=1TO1500:NEXTI:E=1:PRINTSP$:RETURN
665 PRINT"{up}<use CRSR keys to scroll through files >":PRINT"{up}";
670 IFCO=0THENPRINT"<SPACE=select, RETURN=done, CNTRL=abort>"
680 IFCO=1THENPRINT"<hit RETURN to select file, CNTRL=abort>"
690 PRINT"{up}";:TP=1:RL=1:SL=1:GOTO705
700 PRINT"{up*8}";
705 FORI=TPTOTP+7
710 IFI>FLTHENPRINTLEFT$(SP$,17)" [ ]":GOTO730
715 PRINT" ";:IFFL(I)=1THENPRINT"{reverse on}";
720 PRINTFL$(I)+LEFT$(SP$,16-LEN(FL$(I)))+"{reverse off} [ ]"
730 NEXTI:SP=1024+(40*(PEEK(214)-9))
740 S1=SP+(SL*40)+19:POKES1,122
750 GETA$
760 IFA$<>"{up}"THEN800
770 IFRL=1ANDTP=1ANDSL=1THEN750
780 IFSL>1THENSL=SL-1:RL=RL-1:POKES1,32:GOTO740
790 RL=RL-1:TP=TP-1:GOTO700
800 IFA$<>"{down}"THEN840
810 IFRL=FLTHEN750
820 IFSL<8THENSL=SL+1:RL=RL+1:POKES1,32:GOTO740
830 RL=RL+1:TP=TP+1:GOTO700
840 IFA$<>" "ORCO=1THEN880
850 X=128:IFFL(RL)=1THENX=-128:FL(RL)=0
860 FORI=S1-3TOS1-18STEP-1:POKEI,PEEK(I)+X:NEXT:IFX=128THENFL(RL)=1
870 GOTO750
880 IFPEEK(653)=4THENF$="":E=1:FORI=1TOFL:FL(I)=0:FL$(I)="":NEXTI:RETURN
890 IFA$<>CHR$(13)THEN750
900 IFCO=1THENF$=FL$(RL)
910 PRINT"{up*11}";:FORI=1TO11:PRINTSP$;"{space*5}";:NEXTI
915 PRINT"{up*2}":PRINT"{up*11}";
920 RETURN
950 CC=0:IFDV<8THENDV=8
960 DV$=RIGHT$(STR$(DV),2):DR$=RIGHT$(STR$(DR),2)
970 PRINT:PRINT"Device: {reverse on}"+DV$+"{reverse off}{space*2}Drive: {reverse on}"+DR$;"{up}"
975 FORI=1TO7+(CC*11):PRINT"{right}";:NEXTI:PRINT">{up*2}"
980 GETA$:IFA$="{right}"ANDCC=0THENCC=ABS(CC-1):GOTO960
983 IFA$=CHR$(13)THENPRINT"{down}"SP$"{up*2}":RETURN
985 IFA$="{left}"ANDCC=1THENCC=ABS(CC-1):GOTO960
990 X=1:IFA$="{down}"THENX=-1:GOTO995
992 IFA$<>"{up}"THEN980
995 IFCC=1THENDR=DR+X:IFDR<0THENDR=0
997 IFCC=0THENDV=DV+X:IFDV<0THENDV=0
999 GOTO960
1000 GOSUB950:E=0:CO=1:GOSUB600:IFE>0ORF$=""THEN3070
1010 OPEN1,DV,15:OPEN2,DV,2,MID$(STR$(DR),2)+":"+F$+",s,r"
1020 INPUT#1,E:IFE>0THENCLOSE2:CLOSE1:GOTO7
1030 GOSUB6:PRINT"{down}Loading...{up}":INPUT#2,F:IFF=0THEN1050
1040 FORI=1TOF:INPUT#2,F$(I,0),F$(I,1):NEXTI
1045 FORI=1TOFL:FL$(I)="":FL(I)=0:NEXTI:CLOSE2:CLOSE1
1050 PRINT"{up}Files loaded:"+STR$(F)+", hit return.":D0=DV:D1=DR
1060 GETA$:IFA$<>CHR$(13)THEN1060
1070 GOSUB6:PRINT"{down}"SP$"{up}":GOTO7
2000 T0=DV:T1=DR:DV=D0:DR=D1:GOSUB950
2010 PRINT"{down}Filename for list{right*2}";F$:PRINT"{up}"TAB(17);:INPUTF$
2020 PRINT"{up}"SP$"{up}":IFF$=""ORLEN(F$)>16THEN2120
2030 OPEN1,DV,15:OPEN2,DV,2,MID$(STR$(DR),2)+":"+F$+",s,w"
2040 INPUT#1,E:IFE>0THENCLOSE1:CLOSE2:GOTO2100
2050 PRINT"Saving...":PRINT#2,F:FORI=1TOF:PRINT#2,F$(I,0):PRINT#2,(F$(I,1))
2060 NEXTI:CLOSE2:CLOSE1:PRINT"{up}"SP$
2070 PRINT"{up}Files saved:"+STR$(F)+", hit return."
2080 GETA$:IFA$<>CHR$(13)THEN1060
2090 GOSUB6:PRINT"{down}"SP$"{up}":GOTO2120
2100 GOSUB6:O$="File exists, replace? ":C$="yn":GOSUB4500:PRINT
2110 IFYN$="y"THENOPEN1,DV,15,"s"+MID$(STR$(DR),2)+":"+F$:CLOSE1:GOTO2030
2120 DV=T0:DR=T1:GOTO7
3000 PRINT"{down}<Add entries and four letter references>";
3010 PRINT"<{space*2}Hit return on a blank entry to exit >{down}":OPEN1,0
3020 IFF<1000THEN3050
3030 GOSUB6:FORI=1TO11:PRINTSP$;:NEXTI:GOSUB6:PRINT"{down}Buffer full, hit return."
3040 GETA$:IFA$<>CHR$(13)THEN3040
3045 PRINT"{up}"SP$:CLOSE1:GOTO7
3050 PRINT"Entry name{space*2}{reverse on}{space*16}{reverse off}{left*16}{reverse on}";:A=F+1:F$(A,0)=""
3060 INPUT#1,F$(A,0):PRINT:PRINT"{up}"SP$:X=0:Y=16:GOSUB3110:IFE=0THEN3080
3070 GOSUB6:FORI=1TO13:PRINTSP$;:NEXTI:CLOSE1:GOTO7
3080 PRINT"{up}Reference code{space*2}{reverse on}{space*4}{reverse off}{left*4}{reverse on}";:F$(A,1)=""
3090 INPUT#1,F$(A,1):PRINT:PRINT"{up}"SP$"{up}":X=1:Y=4:GOSUB3110:IFE=1THEN3070
3100 F=F+1:GOTO3020
3110 E=0:IFF$(A,X)=""ORLEN(F$(A,X))>YTHENE=1:RETURN
3120 F$(A,X)=F$(A,X)+LEFT$(SP$,Y-LEN(F$(A,X))):RETURN
4000 GOSUB950
4010 OPEN1,0:PRINT"{down}Reference code{space*2}{reverse on}{space*4}{reverse off}{left*4}{reverse on}";:RF$=""
4020 INPUT#1,RF$:PRINT:CLOSE1:PRINT"{up}"SP$"{up}":IFRF$=""ORRF$="{space*4}"THEN7
4030 IFLEN(RF$)<4THENRF$=RF$+LEFT$(SP$,4-LEN(RF$))
4040 E=0:CO=0:GOSUB600:IFE>0THEN3070
4050 IFF=1000THEN3030
4060 FORI=1TOFL:IFFL(I)>0THENF=F+1:F$(F,0)=FL$(I)+LEFT$(SP$,16-LEN(FL$(I)))
4070 F$(F,1)=RF$:FL(I)=0:FL$(I)="":NEXTI:GOTO7
4500 OC=0:PRINT
4510 PRINTO$"(";:IFOC=0THENPRINT"{reverse on}"LEFT$(C$,1)+"{reverse off}/"+RIGHT$(C$,1)+"{reverse off})":GOTO4530
4520 PRINTLEFT$(C$,1)+"/"+"{reverse on}"+RIGHT$(C$,1)+"{reverse off})"
4530 PRINT"{up}";
4540 GETA$:IFA$="{right}"ANDOC=0THENOC=1:GOTO4510
4550 IFA$="{left}"ORA$="{down}"ANDOC=1THENOC=0:GOTO4510
4560 IFA$=CHR$(13)THENYN$=MID$(C$,OC+1,1):PRINTSP$"{space*3}";:GOSUB6:RETURN
4570 GOTO4540
5000 O$="{reverse on}E{reverse off}dit or {reverse on}d{reverse off}elete entries? ":C$="ed":GOSUB4500:CO=1:OO$="Search"
5010 IFYN$="e"THENCO=2
5020 GOTO7003
6000 PRINT"{down}Disk interface:"
6010 PRINT"Hit return on blank line to exit{down}":OPEN1,0
6020 PRINTDV;"{left}>{reverse on} {reverse off}{left}";:CO$=""
6030 GETA$:IFA$=""THEN6030
6040 A=ASC(A$):IFA=13THEN6100
6050 IFA=20ANDCO$>""THENPRINT" "A$A$"{reverse on} {reverse off}{left}";:CO$=LEFT$(CO$,LEN(CO$)-1):GOTO6030
6060 IFA>31ANDA<96THENCO$=CO$+A$:PRINTA$;"{reverse on} {reverse off}{left}";:GOTO6030
6070 IFA>191ANDA<218THENCO$=CO$+A$:PRINTA$;"{reverse on} {reverse off}{left}";:GOTO6030
6080 GOTO6030
6100 PRINT:CLOSE1:PRINT"{up}"SP$"{up}"
6105 IFCO$=""THEN3070
6110 IFVAL(CO$)THENPD=VAL(CO$):IFPD>7ANDPD<30THENDV=PD:GOTO6020
6120 OPEN1,DV,15,CO$
6130 INPUT#1,E1,E1$,E2,E3:CLOSE1:PRINTE1;",";E1$;",";E2;",";E3;">"
6140 GETA$:IFA$=""THEN6140
6150 PRINT"{up}"SP$"{up}":GOTO6020
7000 OO$="View":CO=0
7003 IFF>0THEN7010
7005 PRINT"{down}No files!":FORI=1TO1500:NEXTI:PRINT"{up}"SP$:GOTO7
7010 CC=0:FF=F:IFCO>0THEN7100
7015 O$=OO$+" {reverse on}a{reverse off}ll or by {reverse on}r{reverse off}eference? ":C$="ar":GOSUB4500
7020 IFYN$="a"THEN7100
7030 OPEN1,0
7040 PRINT"{down}Enter reference code{space*2}{reverse on}{space*4}{reverse off}{left*4}";:INPUT#1,CC$:PRINT:CLOSE1
7050 IFCC$="{space*4}"THEN3070
7060 CC=1:PRINT"{up}"SP$:PRINT"{up}Searching..."
7070 FL=0:FORI=1TOF:IFF$(I,1)=CC$ANDFL<300THENFL=FL+1:FL$(FL)=F$(I,0)
7080 NEXTI:PRINT"{up}"SP$"{up*2}":IFFL=0THEN7005
7090 FF=FL
7100 IFCO=0THENPRINT"{down}<use CRSR keys to scroll, RETURN = exit>";
7105 IFCO>0THENPRINT"{down}<use CRSR{sh space}keys,RETURN=select,CNTRL=exit>";
7110 PRINT"<{space*3}use {reverse on}+{reverse off} and {reverse on}-{reverse off} to move page up/down{space*3}>";
7200 TP=1:RL=1:SL=1:GOTO7220
7210 PRINT"{up*8}";
7220 FORI=TPTOTP+7
7230 IFI>FFTHENPRINTSP$"{space*2}":GOTO7290
7240 IFCC=1THENPRINT" "FL$(I):GOTO7290
7250 PRINT" "F$(I,0)+"{reverse off} ["+F$(I,1)+"]{reverse off}"
7290 NEXTI:SP=1024+(40*(PEEK(214)-9))
7300 S1=SP+(SL*40):POKES1,0
7310 GETA$
7320 IFA$<>"{up}"THEN7360
7330 IFRL=1ANDTP=1ANDSL=1THEN7310
7340 IFSL>1THENSL=SL-1:RL=RL-1:POKES1,32:GOTO7300
7350 RL=RL-1:TP=TP-1:GOTO7210
7360 IFA$<>"{down}"THEN7400
7370 IFRL>=FFTHEN7310
7380 IFSL<8THENSL=SL+1:RL=RL+1:POKES1,32:GOTO7300
7390 RL=RL+1:TP=TP+1:GOTO7210
7400 IFA$<>"-"THEN7430
7410 RL=RL-8:TP=TP-8:IFRL<1ORTP<1THENRL=1:TP=1:SL=1
7420 GOTO7210
7430 IFA$<>"+"THEN7470
7440 IFTP+8>FFTHEN7310
7450 TP=TP+8:RL=RL+8:IFRL>FFTHENRL=TP:SL=1
7460 GOTO7210
7470 IFA$<>CHR$(13)THEN7700
7480 IFCO=0THEN3070
7500 GOSUB6:PRINT"{down*2}";:FORI=1TOSL:PRINT"{down}";:NEXTI:IFCO=2THEN7600
7510 PRINT"{up}";:O$="{right*18}Delete entry? ":C$="yn":GOSUB4500
7520 IFYN$="n"THEN7620
7530 IFRL<FFTHEN7550
7540 FF=FF-1:F=FF:IFFF=0THEN3070
7543 RL=RL-1:SL=SL-1:IFSL=0THENTP=TP-1:SL=1
7547 GOTO7620
7550 FORI=RL+1TOFF:F$(I-1,0)=F$(I,0):F$(I-1,1)=F$(I,1):NEXTI:FF=FF-1:F=FF
7560 GOTO7620
7600 PRINT" ";:OPEN1,0:INPUT#1,F$(RL,0):CLOSE1
7610 F$(RL,0)=LEFT$(F$(RL,0)+SP$,16)
7620 GOSUB6:PRINT"{down*11}";:GOTO7210
7700 IFPEEK(653)<>4THEN7310
7710 GOTO3070
8000 O$="Begin sorting, are you sure? ":C$="yn":GOSUB4500
8010 IFYN$="n"THEN7
8020 PRINT"{down}Sorting, please standby...":X=INT(F/256):POKE828,F-(X*256):POKE829,X
8030 SYS49155
8090 PRINT"{up}"SP$"{up}":GOTO7
9000 O$="Print with how many columns? ":C$="12":GOSUB4500:CC=VAL(YN$)
9010 PRINT"{down}Filename for list{right*2}";F$:PRINT"{up}"TAB(17);:INPUTF$
9020 PRINT"{up}"SP$"{up}":IFF$=""ORLEN(F$)>16THEN3070
9025 AA=INT((20-LEN(F$))/2):FF$=LEFT$(SP$,AA)+F$:FF$=FF$+LEFT$(SP$,20-LEN(FF$))
9030 PRINT"Date in MM/DD/YY format{space*2}MM/DD/YY{left*10}";:INPUTDT$
9040 PRINT"{up}"SP$"{up}":IFLEN(DT$)<>8THEN9030
9050 O$="{up}Go ahead with the print? ":C$="yn":GOSUB4500:IFYN$="n"THEN3070
9060 PRINT"{down}Printing, standby...":
9070 OPEN4,4,7
9080 PRINT#4,DT$;TAB(22)CHR$(34)F$CHR$(34)TAB(17)STR$(F)" entries":PRINT#4
9090 X=0:PG=0:PC=1:FORI=1TOF:PG=PG+1
9100 PRINT#4,F$(I,0)" ["F$(I,1)"]";
9110 IFX=0ANDCC=2THENPRINT#4,TAB(17);:X=1:GOTO9199
9120 PRINT#4:X=0:IFPG<(60*CC)THEN9199
9130 PRINT#4,CHR$(12);:PG=0:PC=PC+1
9140 PRINT#4,DT$;TAB(22)CHR$(34)F$CHR$(34)TAB(17)" page"+STR$(PC):PRINT#4
9199 NEXTI:PRINT#4:PRINT#4:CLOSE4
9200 GOTO3070
